/***************************************************************************
   Copyright 2015 Ufora Inc.

   Licensed under the Apache License, Version 2.0 (the "License");
   you may not use this file except in compliance with the License.
   You may obtain a copy of the License at

       http://www.apache.org/licenses/LICENSE-2.0

   Unless required by applicable law or agreed to in writing, software
   distributed under the License is distributed on an "AS IS" BASIS,
   WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
   See the License for the specific language governing permissions and
   limitations under the License.
****************************************************************************/
`hidden
class {
    member mVecOfRows;
    member numRows;
    member numColumns;

    operator new
    (dataframe.DataFrame(df)) {
        let vecOfRows = Vector.range(
            size(df),
            { RowMajorDataFrameRow(df[_]) }
            );
        
        createInstance(
            cls, 
            mVecOfRows: vecOfRows,
            numRows: size(vecOfRows),
            numColumns: size(vecOfRows[0])            
            );
        }
    (filters.IsVector(vecOfRows)) {
        createInstance(
            cls, 
            mVecOfRows: vecOfRows,
            numRows: size(vecOfRows),
            numColumns: size(vecOfRows[0])            
            );        
        }
    (filters.IsVector(vecOfRows), (numRows, numColumns)) {
        createInstance(
            cls,
            mVecOfRows: vecOfRows,
            numRows: numRows,
            numColumns: numColumns
            );
        }
    (RowMajorDataFrame(df)) { df }
        ;

    ...(`size) {
        size(mVecOfRows);
        };

    operator[]
    (ix) {
        mVecOfRows[ix]
        }
    (lowIx, highIx) {
        RowMajorDataFrame(
            mVecOfRows[lowIx, highIx]
            )
        };

    operator match(RowMajorDataFrame _) { (self,) };

    rows: fun() {
        mVecOfRows
        };

    rowApply: fun(f) {
        rows().apply(f)
        };
    
    operator -(RowMajorDataFrame(other)) {
        assertions.assertEqual(numRows, other.numRows)
        assertions.assertEqual(numColumns, other.numColumns)

        RowMajorDataFrame(
            Vector.range(
                numRows,
                fun(rowIx) {
                    let rowTr = [];
                    let selfRow = self[rowIx];
                    let otherRow = other[rowIx]
                    for ix in sequence(numColumns)
                        rowTr = rowTr :: (selfRow[ix] - otherRow[ix])
                    rowTr
                    }
                ),
            (numRows, numColumns)
            );
        };

    };

