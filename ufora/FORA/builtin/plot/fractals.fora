/***************************************************************************
   Copyright 2015 Ufora Inc.

   Licensed under the Apache License, Version 2.0 (the "License");
   you may not use this file except in compliance with the License.
   You may obtain a copy of the License at

       http://www.apache.org/licenses/LICENSE-2.0

   Unless required by applicable law or agreed to in writing, software
   distributed under the License is distributed on an "AS IS" BASIS,
   WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
   See the License for the specific language governing permissions and
   limitations under the License.
****************************************************************************/
#Markdown("""## Fractals

Provides some code for generating fractals.
""");


juliaSet:
fun (c, imageSize:=200, numIterations:=100, scale:=1.0) {
    let (pixel, (width, height)) = smoothJulia(c, imageSize, numIterations);
    plot.image.bmp(pixel, Int64(width*scale), Int64(height*scale))
    };

smoothJulia:
fun(c, samples=200, iterations=500) {
    escapeDiagram(
        julia(c),  // f
        samples,   // pix?
        iterations // maxIter
        )
    };


julia:
fun(c, poly=fun(z,c) { z*z+c }) {
    fun(z) {
        while (true) {
            z = poly(z,c);
            yield z
            }
        }
    };


smoothEscapeTime:  
fun(process, maxIter = 100) {
    let counter = 0;
    let escapeDist = 1000000000.0;
    for z in process {
        let distance = z.sqNorm;
        counter = counter + 1;
        if (distance > escapeDist) {
            return Float64(counter) - 
                ((math.log(distance) - math.log(escapeDist)) / 
                    (math.log(escapeDist)))
            };
        if (counter > maxIter) {
            return maxIter
            }
        }
    };

// Returns a vector and (width, height) tuple.
escapeDiagram:  
fun(f, pix = 50, maxIter = 100, bounds = ((-1.0,1.0),(-1.0,1.0)),
        colorfun =
        fun(c) {
            let res = (10.0 / (1.0 + c)) <<< 1.0;
            c = plot.hsbaToRgba(res, 1.0, 1.0 - res, 1.0);
//          (r:UInt8(c[0]*255), g:UInt8(c[1]*255), b:UInt8(c[2] * 255), a:UInt8(c[3]*255))
            (UInt8(c[0]*255), UInt8(c[1]*255), UInt8(c[2] * 255), UInt8(c[3]*255))
            }
        )
    {
    
    let sampleFun = 
    fun (x, y) {
        let process = f(math.Complex(x,y));
        colorfun(smoothEscapeTime(process, maxIter))
        };
    
    return (
        [sampleFun(x,y)
            for x in sequence(range:bounds[0], samples:pix)
            for y in sequence(range:bounds[1], samples:pix)
            ],
        (pix, pix)
        )
    };


example1:
smoothJulia(
    math.Complex(6.0/20.0, 9.0/20.0),
    850,
    600
    );



