/***************************************************************************
   Copyright 2015 Ufora Inc.

   Licensed under the Apache License, Version 2.0 (the "License");
   you may not use this file except in compliance with the License.
   You may obtain a copy of the License at

       http://www.apache.org/licenses/LICENSE-2.0

   Unless required by applicable law or agreed to in writing, software
   distributed under the License is distributed on an "AS IS" BASIS,
   WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
   See the License for the specific language governing permissions and
   limitations under the License.
****************************************************************************/
`hidden;

Weibull:
#Markdown(
"""### `Weibull`

#### Description

A class for Weibull distributions.

#### Aliases

    math.random.Weibull

""")

class 
	{
#Markdown(
"""#### Description

The scale parameter.
""")
	member lambda_;

#Markdown(
"""#### Description

The shape parameter.
""")
	member k_;

#Markdown(
"""#### Description

The underlying uniform random number generator used for pulling samples from the distribution.
""")
	member unifRng_;

#Markdown(
"""#### Usage

    math.random.Weibull(lambda, k, seed, rngFun = math.random.MersenneTwister)

#### Description

Creates a Weibull distribution object with scale parameter `lambda` and shape 
parameter `k` using an integral random number generator set by `rngFun` to be 
seeded with seed `seed`. 
""")
	operator new(lambda, k, seed, rngFun = math.random.MersenneTwister)
		{
		checkParams_(lambda, k);

		let unifRng = UniformReal(0, 1, seed, rngFun);

		createInstance(cls, lambda_: lambda, k_: k, unifRng_: unifRng)
		};		
		
#Markdown(
"""#### Usage

    iterator(weibullInstance)

#### Description

Produces an `iterator` from an instance of `math.random.Weibull`, `weibullInstance`, 
which can be used to generate values from the represented distribution.
""")
	operator iterator()
		{
		for x in unifRng_
			yield quantile(x, lambda_, k_);
		};

	static pdf:
#Markdown(
"""#### Usage

    math.random.Weibull.pdf(x, lambda, k)

#### Description

Evaluates the pdf of a Weibull distribution with scale parameter
`lambda` and shape parameter `k` at `x`.
""")
	fun(x, lambda, k)
		{
		checkParams_(lambda, k);

		if (x < 0)
			return 0.0;

		return (k / Float64(lambda)) * ((x / Float64(lambda)) ** (k - 1.0)) * 
			   	  exp(-((x / Float64(lambda)) ** k));
		};
		
	static cdf:
#Markdown(
"""#### Usage

    math.random.Weibull.cdf(x, lambda, k)

#### Description

Evaluates the cdf of a Weibull distribution with scale parameter
`lambda` and shape parameter `k` at `x`.
""")
	fun(x, lambda, k)
		{
		checkParams_(lambda, k);
		if (x < 0)
			return 0.0;
		return 1.0 - exp(-((x / Float64(lambda)) ** k));			
		};		
		
	static quantile:
#Markdown(
"""#### Usage

    math.random.Weibull.quantile(x, lambda, k)

#### Description

Evaluates the quantile of a Weibull distribution with scale parameter
lambda and shape parameter k at x.
""")
	fun(x, lambda, k)
		{
		checkParams_(lambda, k);
		if (x <= 0 or x >= 1)
			throw "x must be between 0 and 1";
		return ((-1.0) * log(1.0 - x)) ** (1.0 / Float64(k)) * lambda;	
		};		

	static checkParams_:
#Markdown(
"""#### Usage

    math.random.Weibull.checkParams_(lambda, k)

#### Description

Checks if the parameters lambda and k are admissible.
""")
	fun(lambda, k)
		{
		if (lambda <= 0 or k <= 0)
			throw "parameters must be strictly positive";
		};			

	};



