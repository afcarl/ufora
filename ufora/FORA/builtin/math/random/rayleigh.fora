/***************************************************************************
   Copyright 2015 Ufora Inc.

   Licensed under the Apache License, Version 2.0 (the "License");
   you may not use this file except in compliance with the License.
   You may obtain a copy of the License at

       http://www.apache.org/licenses/LICENSE-2.0

   Unless required by applicable law or agreed to in writing, software
   distributed under the License is distributed on an "AS IS" BASIS,
   WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
   See the License for the specific language governing permissions and
   limitations under the License.
****************************************************************************/
`hidden;

Rayleigh:
#Markdown(
"""### `Rayleigh`

#### Description

A class for Rayleigh distributions.

#### Aliases

    math.random.Rayleigh

""")

class
	{
#Markdown(
"""#### Description

The `sigma` parameter of the distribution.
""")
	member sigma_;

#Markdown(
"""#### Description

The underlying uniform random number generator used for pulling samples from the distribution.
""")
	member unifRng_;

#Markdown(
"""#### Description

    math.random.Rayleigh(sigma, seed, rngFun = math.random.MersenneTwister)

#### Description

Creates a Rayleigh distribution object with parameter `sigma` using an integral
random number generator set by `rngFun` to be seeded with seed `seed`. 
""")
	operator new(sigma, seed, rngFun = math.random.MersenneTwister)
		{
		checkParam_(sigma);
		let unifRng = UniformReal(0, 1, seed, rngFun);
		createInstance(cls, sigma_: sigma, unifRng_: unifRng)
		};

#Markdown(
"""#### Usage

    iterator(rayleighInstance)

#### Description

Produces an `iterator` from an instance of `math.random.Rayleigh`, `rayleighInstance`, 
which can be used to generate values from the represented distribution.
""")
	operator iterator()
		{
		for x in unifRng_
			yield quantile(x, sigma_);
		};

	static pdf:
#Markdown(
"""#### Usage

    math.random.Rayleigh.pdf(x, sigma)

#### Description

Evaluates the pdf of a Rayleigh distribution with parameter `sigma` at `x`.
""")
	fun(x, sigma)
		{
		checkParam_(sigma);
		if (x < 0)
			return 0.0;

		return (x / (sigma ** 2.0)) * exp(-(x ** 2.0) / (2.0 * (sigma ** 2.0)));
		};
		
	static cdf:
#Markdown(
"""#### Usage

    math.random.Rayleigh.cdf(x, sigma)

#### Description

Evaluates the cdf of a Rayleigh distribution with parameter `sigma` at `x`.
 """)
	fun(x, sigma)
		{	
		checkParam_(sigma);
		if (x < 0)
			return 0.0;
		return 1.0 - exp(-(x ** 2.0) / (2.0 * (sigma ** 2.0)));
		};		
		
	static quantile:
#Markdown(
"""#### Usage

    math.random.Rayleigh.quantile(x, sigma)

#### Description

Evaluates the quantile of a Rayleigh distribution with parameter `sigma` at `x`. 
""")
	fun(x, sigma)
		{
		checkParam_(sigma);
		if (x <= 0 or x >= 1)
			throw "x must be between 0 and 1";
		return sigma * sqrt((-1.0) * log(1.0 - x) * 2.0);	
		};		
		
	static checkParam_:
#Markdown(
"""#### Usage

    math.random.Rayleigh.checkParam_(sigma)

#### Description

Checks if the parameter `sigma` is admissible.
""")
	fun(sigma)
		{
		if (sigma <= 0)
			throw "sigma must be strictly positive";
		};			

	};



