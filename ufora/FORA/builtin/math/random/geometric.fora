/***************************************************************************
   Copyright 2015 Ufora Inc.

   Licensed under the Apache License, Version 2.0 (the "License");
   you may not use this file except in compliance with the License.
   You may obtain a copy of the License at

       http://www.apache.org/licenses/LICENSE-2.0

   Unless required by applicable law or agreed to in writing, software
   distributed under the License is distributed on an "AS IS" BASIS,
   WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
   See the License for the specific language governing permissions and
   limitations under the License.
****************************************************************************/
`hidden;

Geometric:
#Markdown(
"""### `Geometric`

#### Description

A class for geometric distributions.

#### Aliases

    math.random.Geometric

""")

class 
	{
#Markdown(
"""#### Description

The success probability.
""")
	member p_;

#Markdown(
"""#### Description

The underlying uniform random number generator used for pulling samples from the distribution.
""")
	member unifRng_;

#Markdown(
"""#### Usage

    math.random.Geometric(p, seed, rngFun = math.random.MersenneTwister)

#### Description

Creates a geometric random variable object with success probability 
`p`, using an integral random number generator set by `rngFun` to be seeded with
seed `seed`.
""")
	operator new(p, seed, rngFun = math.random.MersenneTwister)
		{
		checkParam_(p);
		let unifRng = UniformReal(0, 1, seed, rngFun);
		createInstance(cls, p_: p, unifRng_: unifRng)
		};

#Markdown(
"""#### Usage

    iterator(geometricInstance)

#### Description

Produces an `iterator` from an instance of `math.random.Geometric`, `geometricInstance`, 
which can be used to generate values from the represented distribution.
""")
	operator iterator()
		{
		for x in unifRng_
			yield quantile(x, p_)
		};

	static pdf:
#Markdown(
"""#### Usage

    math.random.Geometric.pdf(x, p)

#### Description

Returns the pdf of a geometric random variable with parameter `p` 
evaluated at `x`.
""")
	fun(x, p)
		{
		checkParam_(p);
		if (x < 0)
			return 0.0;

		if (x == math.floor(x))
			{
			return p * ((1.0 - p) ** x);
			}
		else
			{
			return 0.0;
			}
		};
		
	static cdf:
#Markdown(
"""#### Usage

    math.random.Geometric.cdf(x, p)

#### Description

Returns the cdf of a geometric random variable with parameter `p` 
evaluated at `x`.
""")
	fun(x, p)
		{
		checkParam_(p);
		if (x < 0)
		    return 0.0;
		return 1.0 - (1.0 - p) ** (floor(x) + 1.0);
		};
	
	static quantile:
#Markdown(
"""#### Usage

    math.random.Geometric.quantile(x, p)

#### Description

Returns the quantile of a geometric random variable with parameter `p`
 evaluated at x.
""")
	fun(x, p)
		{
		checkParam_(p);
		if (x <= 0 or x >= 1)
		    throw "x must be in (0,1)";
		return math.ceil(math.log(1.0 - x) / math.log(1.0 - p) - 1.0);		
		};

	static checkParam_:
#Markdown(
"""#### Usage

    math.random.Geometric.checkParam_(p)

#### Description

Checks if the parameter `p` is admissible.
""")
	fun(p)
		{
		if (p < 0 or p > 1)
			throw "p must be in [0, 1]";
		};	

	};


