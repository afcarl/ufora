/***************************************************************************
   Copyright 2015 Ufora Inc.

   Licensed under the Apache License, Version 2.0 (the "License");
   you may not use this file except in compliance with the License.
   You may obtain a copy of the License at

       http://www.apache.org/licenses/LICENSE-2.0

   Unless required by applicable law or agreed to in writing, software
   distributed under the License is distributed on an "AS IS" BASIS,
   WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
   See the License for the specific language governing permissions and
   limitations under the License.
****************************************************************************/
`hidden;

ExtremeValue:
#Markdown(
"""### `ExtremeValue`

#### Description

A class for extreme value (ie Gumbel) distributions.

#### Aliases

    math.random.ExtremeValue

""")

class
    {
#Markdown(
"""#### Description

The location parameter.
""")
	member mu_;

#Markdown(
"""#### Description

The scale parameter.
""")
	member beta_;

#Markdown(
"""#### Description

The underlying uniform random number generator used for pulling samples from the distribution.
""")
	member unifRng_;

#Markdown(
"""#### Usage

    math.random.ExtremeValue(mu, beta, seed, rngFun = math.random.MersenneTwister)`

#### Description

Creates an extreme value (aka Gumbel) distribution object with location
parameter `mu` and scale parameter `beta` using an integral random number
generator set by `rngFun` to be seeded with seed `seed`.
""")
	operator new(mu, beta, seed, rngFun = math.random.MersenneTwister)
		{
		checkParams_(mu, beta);
		let unifRng = UniformReal(0, 1, seed, rngFun);
		createInstance(cls, mu_: mu, beta_: beta, unifRng_: unifRng);
		};

#Markdown(
"""#### Usage

    iterator(extremeValueInstance)

#### Description

Produces an `iterator` from an instance of `math.random.ExtremeValue`, `extremeValueInstance`, 
which can be used to generate values from the represented distribution.
""")
	operator iterator()
		{
		for i in unifRng_
			yield quantile(i, mu_, beta_);
		};

	static pdf:
#Markdown(
"""#### Usage

    math.random.ExtremeValue.pdf(x, mu, beta)

#### Description

Evaluates the pdf of a extreme value distribution with location 
parameter `mu` and scale parameter `beta` at `x`.
""")
	fun(x, mu, beta)
		{
		checkParams_(mu, beta);
		let z = (x - mu) / Float64(beta);
		return (1.0 / beta) * exp(-z - exp(-z));
		};
		
	static cdf:
#Markdown(
"""`cdf(x, mu, beta)`

Evaluates the cdf of a extreme value distribution with location 
parameter `mu` and scale parameter `beta` at `x`.
""")
	fun(x, mu, beta)
		{
		checkParams_(mu, beta);
		return exp(-exp(-(x - mu) / Float64(beta)));
		};		
		
	static quantile:
#Markdown(
"""#### Usage

    math.random.ExtremeValue.quantile(x, mu, beta)

#### Description

Evaluates the quantile of a extreme value distribution with location 
parameter `mu` and scale parameter `beta` at `x`.
""")
	fun(x, mu, beta)
		{
		checkParams_(mu, beta);
		if (x <= 0 or x >= 1)
			throw "x must be between 0 and 1";
		return (-1.0) * beta * log(-log(x)) + mu;	
		};		
		
static checkParams_:
#Markdown(
"""#### Usage

    math.random.ExtremeValue.checkParams_(mu, beta)

#### Description

Checks that the parameters are admissible.
""")
	fun(mu, beta)
		{
		if (beta <= 0)
			throw "beta must be strictly positive";
		};	


	};





