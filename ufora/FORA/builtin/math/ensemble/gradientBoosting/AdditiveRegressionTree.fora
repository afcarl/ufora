/***************************************************************************
   Copyright 2015 Ufora Inc.

   Licensed under the Apache License, Version 2.0 (the "License");
   you may not use this file except in compliance with the License.
   You may obtain a copy of the License at

       http://www.apache.org/licenses/LICENSE-2.0

   Unless required by applicable law or agreed to in writing, software
   distributed under the License is distributed on an "AS IS" BASIS,
   WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
   See the License for the specific language governing permissions and
   limitations under the License.
****************************************************************************/
#Markdown("""### AdditiveRegressionTree

#### Description

An ensemble model comprised of a sequence of trees whose 
prediction value is the sum of the values of the predictions of 
the constituent trees.

""")
class {
    member mTrees;

    numTrees: fun() { size(mTrees) };

    trees: fun() { mTrees };

    getTree: fun(ix) { mTrees[ix] };

    operator new() { createInstance(cls, mTrees: []) };

    operator +(tree) {
        createInstance(
            cls, mTrees: mTrees :: tree
            )
        };

    predict:
    fun
    (dataframe.DataFrame(df), nEstimators:= nothing) {
        df.rowApply(
            fun(row) { self.predictionFunction_(row, nEstimators: nEstimators) }
            )
        }
    (row, nEstimators:= nothing) {
        self.predictionFunction_(row, nEstimators: nEstimators)
        };

    predictionFunction_: fun(row, nEstimators:= nothing) {
        if (nEstimators is nothing)
            nEstimators = size(mTrees);

        mTrees[, nEstimators].apply({ _.predict(row) }).sum()
        };

    mixin predictorMixin;
    };

